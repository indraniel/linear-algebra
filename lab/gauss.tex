\chapter{Gauss's Method}

\Sage{} can solve linear systems in a number of ways.
The first way is to use a general system solver, one not specialized to linear
systems.
The second way is to leverage the special advantages of linear systems.
We'll see both. 



\section{Systems}
To enter a system of equations you must first enter single equations.
So you must start with variables.
We have seen one kind of variable in giving commands like these.
\begin{sageoutput}
x = 3
7*x
\end{sageoutput}
Here $x$ is the name of a location to hold values.
Variables in equations are something different; in the equation
$C=2\pi\cdot r$ the two variables do not have fixed values, nor
are they tied to a location in the computer's memory. 

To illustrate the difference enter an unassigned 
variable.\footnote{These output blocks are taken directly from \protect\Sage{}
without any by-hand copying and pasting.
This has the advantage that 
there is no chance of confusion caused by author-introduced differences
between what this manual showy and what \protect\Sage{} actually does.
But it has two drawbacks.
First, \protect\Sage{} puts out some lines too long to fit in the block.
Sometimes these lines will be split and have their tail 
carried to the next line, as here, 
while sometimes they will be truncated if the extra characters are unimportant.
The second drawback is that \protect\Sage{} occasionally gives 
system-specific responses, as in the line here giving the directory.
Naturally the response on your system will show your directory.
This is the first time in this manual 
that the issue of these artifacts appears; we won't note them again.}
\begin{sageoutput}[s,1,71,67;s,2,70,66]
y
\end{sageoutput}
\noindent
\Sage{} defaults to expecting that
$y$ is the name of a location to hold a value.
Before you use it as a symbolic variable, you must first
warn the system.
\begin{sageoutput}
var('x,y')
y
2*y
k = 3
2*k
\end{sageoutput}
\noindent
Because we haven't told \Sage{} otherwise, it takes $k$ as a location to hold
values and it evaluates \inlinecode{2*k}.
But it does not evaluate \inlinecode{2*y} because $y$ is a symbolic variable.

With that, a system of equations is a list.
\begin{sageoutput}
var('x,y,z')                                  
eqns = [x-y+2*z == 4, 2*x+2*y == 12, x-4*z==5]
\end{sageoutput}
\noindent
You must write double equals \inlinecode{==} in the equations instead of 
the assignment operator \inlinecode{=}.
Also, you must write \inlinecode{2*x}
instead of \inlinecode{2x}.
Either mistake will trigger a  
\inlinecode{SyntaxError: invalid syntax}.

Solve the system with \Sage's general-purpose solver.
\begin{sageoutput}[d,0,2]
var('x,y,z')                                  
eqns = [x-y+2*z == 4, 2*x+2*y == 12, x-4*z==5]
solve(eqns, x,y,z)                            
\end{sageoutput}

You can put a parameter in the right side and solve for the variables
in terms of the parameter.
\begin{sageoutput}
var('x,y,z,a')                                
eqns = [x-y+2*z == a, 2*x+2*y == 12, x-4*z==5]
solve(eqns, x,y,z) 
\end{sageoutput}



\subsection{Matrices}
The \textit{solve} routine is general-purpose but 
for the special case of linear systems matrix notation is the best tool.
 
Most of the matrices in the book
have entries that are fractions so here we stick
to those.
\Sage{} uses `QQ' for the rational numbers,
`RR' or 'RDF' for real numbers,
`CC' for the complex numbers,
and
`ZZ' for the integers.

Enter a row of the matrix as a list, and
enter the entire matrix as a list of rows.
\begin{sageoutput}
M = matrix(QQ, [[1, 2, 3], [4, 5, 6], [7 ,8, 9]])
M
M[1,2]
M.nrows()
M.ncols()
\end{sageoutput}
\noindent
\Sage{} lists are zero-indexed, as with \python{} lists, 
so \inlinecode{M[1,2]} asks
for the entry in the second row and third column. 

Enter a vector in much the same way.
\begin{sageoutput}
v = vector(QQ, [2/3, -1/3, 1/2])
v
v[1]
\end{sageoutput}
\noindent
\Sage{} does not worry too much about the distinction between row and column
vectors.
Note however that
it appears with rounded brackets so that 
it looks different than a one-row matrix.

You can augment a matrix with a vector.
\begin{sageoutput}
M = matrix(QQ, [[1, 2, 3], [4, 5, 6], [7 ,8, 9]])
v = vector(QQ, [2/3, -1/3, 1/2])
M_prime = M.augment(v)
M_prime
\end{sageoutput}
\noindent
You can use an optional argument to
have \Sage{} remember the distinction between the two parts 
of $M^\prime$.
\begin{sageoutput}[d,0,2]
M = matrix(QQ, [[1, 2, 3], [4, 5, 6], [7 ,8, 9]])
v = vector(QQ, [2/3, -1/3, 1/2])
M_prime = M.augment(v,subdivide=True)
M_prime                              
\end{sageoutput}



\subsection{Row operations}
Computers are good for jobs that are tedious and error-prone.
Row operations are both.

% First enter an example matrix.
\begin{sageoutput}
M = matrix(QQ, [[0, 2, 1], [2, 0, 4], [2 ,-1/2, 3]])
v = vector(QQ, [2, 1, -1/2])                        
M_prime = M.augment(v, subdivide=True)              
M_prime                                             
\end{sageoutput}
\noindent
Swap the top rows (remember that row indices start at zero).
\begin{sageoutput}[d,0,3]
M = matrix(QQ, [[0, 2, 1], [2, 0, 4], [2 ,-1/2, 3]])
v = vector(QQ, [2, 1, -1/2])                        
M_prime = M.augment(v, subdivide=True)              
M_prime.swap_rows(0,1)
M_prime
\end{sageoutput}
\noindent
Rescale the top row.
\begin{sageoutput}[d,0,4]
M = matrix(QQ, [[0, 2, 1], [2, 0, 4], [2 ,-1/2, 3]])
v = vector(QQ, [2, 1, -1/2])                        
M_prime = M.augment(v, subdivide=True)              
M_prime.swap_rows(0,1)
M_prime.rescale_row(0, 1/2)
M_prime
\end{sageoutput}
\noindent
Get a new  bottom row by adding $-2$~times the top to the current bottom
row.
\begin{sageoutput}[d,0,5]
M = matrix(QQ, [[0, 2, 1], [2, 0, 4], [2 ,-1/2, 3]])
v = vector(QQ, [2, 1, -1/2])                        
M_prime = M.augment(v, subdivide=True)              
M_prime.swap_rows(0,1)
M_prime.rescale_row(0, 1/2)
M_prime.add_multiple_of_row(2,0,-2)
M_prime
\end{sageoutput}
\noindent
Finish by finding echelon form.
\begin{sageoutput}[d,0,6]
M = matrix(QQ, [[0, 2, 1], [2, 0, 4], [2 ,-1/2, 3]])
v = vector(QQ, [2, 1, -1/2])                        
M_prime = M.augment(v, subdivide=True)              
M_prime.swap_rows(0,1)
M_prime.rescale_row(0, 1/2)
M_prime.add_multiple_of_row(2,0,-2)
M_prime.add_multiple_of_row(2,1,1/4)
M_prime                             
\end{sageoutput}
\noindent
By the way,
\Sage{} would have given us echelon form in a single operation 
had we run the command
\inlinecode{M_prime.echelon_form()}.

Now by-hand back substitution gives the solution, or we can 
use \Sagecmd{solve}.
\begin{sageoutput}[d,0,7]
M = matrix(QQ, [[0, 2, 1], [2, 0, 4], [2 ,-1/2, 3]])
v = vector(QQ, [2, 1, -1/2])                        
M_prime = M.augment(v, subdivide=True)              
M_prime.swap_rows(0,1)
M_prime.rescale_row(0, 1/2)
M_prime.add_multiple_of_row(2,0,-2)
M_prime.add_multiple_of_row(2,1,1/4)
var('x,y,z')
eqns=[-3/4*z == -1, 2*y+z == 2, x+2*z == 1/2]
solve(eqns, x, y, z)
\end{sageoutput}

The operations \Sagecmd{swap\_rows},
\Sagecmd{rescale\_rows}, and \Sagecmd{add\_multiple\_of\_row}
changed the matrix $M^\prime$.
\Sage{} has related commands that return a changed matrix
but leave the starting matrix unchanged.
\begin{sageoutput}
M = matrix(QQ, [[1/2, 1, -1], [1, -2, 0], [2 ,-1, 1]])
v = vector(QQ, [0, 1, -2])
M_prime = M.augment(v, subdivide=True) 
M_prime
N = M_prime.with_rescaled_row(0,2)
M_prime
N      
\end{sageoutput}
\noindent
Here, $M^\prime$ is unchanged by the routine while $N$ is the returned 
changed matrix.
The other two routines of this kind are \Sagecmd{with\_swapped\_rows} 
and \Sagecmd{with\_added\_multiple\_of\_row}.




\subsection{Nonsingular and singular systems}
Resorting to \Sagecmd{solve} after going through the row operations is artless.
\Sage{} will give reduced row echelon form straight from the augmented matrix.
\begin{sageoutput}
M = matrix(QQ, [[1/2, 1, -1], [1, -2, 0], [2 ,-1, 1]])
v = vector(QQ, [0, 1, -2])
M_prime = M.augment(v, subdivide=True) 
M_prime.rref()
\end{sageoutput}

In that example the matrix~$M$ on the left is nonsingular because it is 
square and because Gauss's Method produces echelon forms where every one of
$M$'s columns has a leading variable.
The next example starts with a different square matrix, a 
singular matrix, and consequently leads to 
echelon form systems where there are columns on the left 
that do not have a leading variable. 
\begin{sageoutput}
M = matrix(QQ, [[1, 1, 1], [1, 2, 3], [2 , 3, 4]])    
v = vector(QQ, [0, 1, 1]) 
M_prime = M.augment(v, subdivide=True)
M_prime
M_prime.rref()
\end{sageoutput}
\noindent
Recall that the singular case has two subcases.
The first is above: because in echelon form
every row that is all zeros on the
left has an entry on the right that is also zero,
the system has infinitely many solutions.
In contrast, with the same starting matrix
the example below has a row that is zeros on the left but is nonzero
on the right and so the system has no solution.
\begin{sageoutput}[d,0,1]
M = matrix(QQ, [[1, 1, 1], [1, 2, 3], [2 , 3, 4]])    
v = vector(QQ, [0, 1, 2])             
M_prime = M.augment(v, subdivide=True)
M_prime.rref()                        
\end{sageoutput}
\noindent
The difference between the subcases
has to do with the relationships among  
the rows of~$M$ and the relationships among the rows of the vector.
In both cases, the relationship among the rows of the matrix~$M$
is that the first two rows add to the third.
In the first case the vector has the same relationship while in the second
it does not.

The easy way to ensure that a zero row in the matrix 
on the left is associated with a zero
entry in the vector on the right is to make the vector have all zeros, that is,
to consider the homogeneous system associated with~$M$.
\begin{sageoutput}
v = zero_vector(QQ, 3)
v
M = matrix(QQ, [[1, 1, 1], [1, 2, 3], [2 , 3, 4]]) 
M_prime = M.augment(v, subdivide=True)
M_prime
M_prime.rref()
\end{sageoutput}

You can get the numbers of the columns having leading entries with 
the \Sagecmd{pivots} method
(there is a complementary \Sagecmd{nonpivots}).
\begin{sageoutput}[d,0,3]
M = matrix(QQ, [[1, 1, 1], [1, 2, 3], [2 , 3, 4]]) 
v = vector(QQ, [0, 1, 2])
M_prime = M.augment(v, subdivide=True)
M_prime                  
M_prime.pivots()         
M_prime.rref()
\end{sageoutput}
\noindent
Because column~$2$ is not in the list of pivots we know that the
system is singular before we see it in echelon form.

We can use this observation to write a routine that decides if a 
square matrix is nonsingular.
% The return here causes an error I don't understand from runrepl.
% \begin{sageoutput}
% def check_nonsingular(mat):  
%     if not(mat.is_square()):
%         print "ERROR: mat should be square"
%         return
%     p = mat.pivots()
%     for col in range(mat.ncols()):
%         if not(col in p):
%             print "nonsingular"
%             break
         
% N = Matrix(QQ, [[1, 2, 3], [4, 5, 6], [7, 8, 9]])
% check_nonsingular(N)                                
% N = Matrix(QQ, [[1, 0, 0], [0, 1, 0], [0, 0, 1]])
% check_nonsingular(N)                                   
% \end{sageoutput}
\begin{lstlisting}
sage: def check_nonsingular(mat):
....:     if not(mat.is_square()):
....:         print "ERROR: mat must be square"
....:         return
....:     p = mat.pivots()
....:     for col in range(mat.ncols()):
....:         if not(col in p):
....:             print "nonsingular"
....:             break
....:          
sage: N = Matrix(QQ, [[1, 2, 3], [4, 5, 6], [7, 8, 9]])
sage: check_nonsingular(N)                                
nonsingular
sage: N = Matrix(QQ, [[1, 0, 0], [0, 1, 0], [0, 0, 1]])
sage: check_nonsingular(N)                                   
\end{lstlisting}
\noindent
Actually, \Sage{} matrices already have a method \Sagecmd{is\_singular}
but this illustrates how you can write routines to extend \Sage.






\subsection{Parametrization}
You can use \Sagecmd{solve} to give the solution set of a system
with infinitely many solutions.
Start with the square matrix of coefficients from above,
where the top two rows add to the bottom row,
and adjoin a vector satisfying the same relationship to get
a system with infinitely many solutions.
Then convert that to a system of equations.
\begin{sageoutput}
M = matrix(QQ, [[1, 1, 1], [1, 2, 3], [2 , 3, 4]])    
v = vector(QQ, [1, 0, 1])                            
M_prime = M.augment(v, subdivide=True)               
M_prime
var('x,y,z')          
eqns = [x+y+z == 1, x+2*y+3*z == 0, 2*x+3*y+4*z == 1]
solve(eqns, x, y)   
solve(eqns, x, y, z)                                 
\end{sageoutput}
The first of the two \Sagecmd{solve} calls asks \Sage{} 
to solve only for $x$ and~$y$ and so the solution is in terms of~$z$.
In the second call \Sage{} produces a parameter of its own.   




%========================================
\section{Automation}

We finish by showing two routines to automate the by-hand row reductions 
of the kind that the text has in the homework.
These use the matrix capabilities of \Sage{} to both describe 
and perform the row operations that bring a matrix to 
echelon form or to reduced echelon form. 

\subsection{Loading and running}
The source file of the script is below, at the end. 
First here are a few sample calls.
Start \Sage{} in the directory containing the file \path{gauss_method.sage}.
\begin{sageoutput}
load "gauss_method.sage"
M = matrix(QQ, [[1/2, 1, 4], [2, 4, -1], [1, 2, 0]])          
v = vector(QQ, [-2, 5, 4])
M_prime = M.augment(v, subdivide=True)  
gauss_method(M_prime)
\end{sageoutput}
% \begin{lstlisting}
% sage: load "gauss_method.sage"
% sage: M = matrix(QQ, [[1/2, 1, 4], [2, 4, -1], [1, 2, 0]])          
% sage: v = vector(QQ, [-2, 5, 4])
% sage: M_prime = M.augment(v, subdivide=True)  
% sage: gauss_method(M_prime)
% [1/2   1   4| -2]
% [  2   4  -1|  5]
% [  1   2   0|  4]
%  take -4 times row 1 plus row 2
%  take -2 times row 1 plus row 3
% [1/2   1   4| -2]
% [  0   0 -17| 13]
% [  0   0  -8|  8]
%  take -8/17 times row 2 plus row 3
% [  1/2     1     4|   -2]
% [    0     0   -17|   13]
% [    0     0     0|32/17]
% \end{lstlisting}

Because the matrix has rational number elements the operations are 
exact\Dash without floating point issues.

The remaining examples skip the steps to make an augmented matrix. 
\begin{sageoutput}[d,0,1]
load "gauss_method.sage"
M1 = matrix(QQ, [[2, 0, 1, 3], [-1, 1/2, 3, 1], [0, 1, 7, 5]])
gauss_method(M1)
\end{sageoutput}

The script also has a routine to go all the way to reduced echelon form.
\begin{sageoutput}[d,0,1]
load "gauss_method.sage"
r1 = [1, 2, 3, 4]
r2 = [1, 2, 3, 4]
r3 = [2, 4, -1, 5]
r4 = [1, 2, 0, 4]
M2 = matrix(QQ, [r1, r2, r3, r4])
gauss_jordan(M2)            
\end{sageoutput}
% \begin{lstlisting}
% sage: M2 = matrix(QQ,
% ....:             [[1, 2, 3, 4], [1, 2, 3, 4],
% ....:              [2, 4, -1, 5], [1, 2, 0, 4]])
% sage: gauss_jordan(M2)            
% [ 1  2  3  4]
% [ 1  2  3  4]
% [ 2  4 -1  5]
% [ 1  2  0  4]
%  take -1 times row 1 plus row 2
%  take -2 times row 1 plus row 3
%  take -1 times row 1 plus row 4
% [ 1  2  3  4]
% [ 0  0  0  0]
% [ 0  0 -7 -3]
% [ 0  0 -3  0]
%  swap row 2 with row 3
% [ 1  2  3  4]
% [ 0  0 -7 -3]
% [ 0  0  0  0]
% [ 0  0 -3  0]
%  take -3/7 times row 2 plus row 4
% [  1   2   3   4]
% [  0   0  -7  -3]
% [  0   0   0   0]
% [  0   0   0 9/7]
%  swap row 3 with row 4
% [  1   2   3   4]
% [  0   0  -7  -3]
% [  0   0   0 9/7]
% [  0   0   0   0]
%  take -1/7 times row 2
%  take 7/9 times row 3
% [  1   2   3   4]
% [  0   0   1 3/7]
% [  0   0   0   1]
% [  0   0   0   0]
%  take -4 times row 3 plus row 1
%  take -3/7 times row 3 plus row 2
% [1 2 3 0]
% [0 0 1 0]
% [0 0 0 1]
% [0 0 0 0]
%  take -3 times row 2 plus row 1
% [1 2 0 0]
% [0 0 1 0]
% [0 0 0 1]
% [0 0 0 0]
% \end{lstlisting}

These are naive implementations of Gauss's Method that are just for fun.
(For instance they don't handle real numbers, just rationals.)
But they do illustrate the point made in this manual's Preface, 
since a person
builds intuition by doing a reasonable number of reasonably hard Gauss's Method
reductions by hand, and at that point automation can take over.


\subsection{Source of \protect\path{gauss_method.sage}}
\textit{Code comment:} lines~50 and~88 are too long
for this page so they end with a 
slash~\inlinecode{\\} to make \python{} continue on the
next line. 

\lstinputlisting{gauss_method.sage}
\endinput


TODO:
